#Quais empresas de CNAEs de incidência de ICMS existem na RFB e não existem na SEFAZ?
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?empresa ?empresa_ ?atividadeICMS WHERE {
    
    ?empresa a sefazma:Estabelecimento_RFB;
             rdfs:label ?empresa_;
      	sefazma:tem_atividade_economica_principal|sefazma:tem_atividade_economica_secundaria ?atividadeICMS.

    ?atividadeICMS a sefazma:Atividade_ICMS.
    
  MINUS {
	?empresa owl:sameAs ?empresaSEFAZ.       
    ?empresaSEFAZ a sefazma:Empresa_Cadastro.
  }

}

#Quais empresas na RFB ou SEFAZ não tem mais sócio pessoa física?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?o1 ?empresa WHERE {
{
    ?o1 a sefazma:Empresa_RFB;
        sefazma:cnpj ?empresa.
    
    MINUS {
    	?o1 sefazma:tem_sociedade_fisica ?x.
    }
 }
 UNION
 {
     ?o1 a sefazma:Empresa_Cadastro;
        sefazma:cnpj ?empresa.
	
	 MINUS {
	 	?o1 sefazma:tem_sociedade ?x.
		?x a sefazma:Sociedade_PF.
     }   
 }
}

#Quais empresas não estão ativas na RFB, mas estão na SEFAZ?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?cnpj_estabelecimento_rfb ?nome_fantasia_rfb ?cnpj_estabelecimento_cadastro ?nome_fantasia_cadastro ?tipo_situacao_rfb ?tipo_situacao_cadastro WHERE {

  ?estabelecimento_rfb a sefazma:Estabelecimento_RFB;
     sefazma:cnpj ?cnpj_estabelecimento_rfb;
     sefazma:nome_fantasia ?nome_fantasia_rfb;
     sefazma:tem_situacao_cadastral ?situacao_cadastral_rfb.
  
  ?situacao_cadastral_rfb rdfs:label ?tipo_situacao.
    FILTER(!CONTAINS(?tipo_situacao, 'ATIVA'))
    BIND(REPLACE(STR(?tipo_situacao),"(\\d|\\-|\\_)","") as ?tipo_situacao_rfb) 
    
 ?estabelecimento_cadastro a sefazma:Estabelecimento_Cadastro;
        sefazma:cnpj ?cnpj_estabelecimento_cadastro;
        sefazma:nome_fantasia ?nome_fantasia_cadastro;
        sefazma:tem_situacao_cadastral ?situacao_cadastral_cadastro.
  
  ?situacao_cadastral_cadastro sefazma:tipo_situacao ?tipo_situacao_cadastro.
    
  ?estabelecimento_rfb owl:sameAs  ?estabelecimento_cadastro.
    FILTER((?tipo_situacao_rfb != ?tipo_situacao_cadastro) && ?tipo_situacao_cadastro = 'ATIVA')


}


#Quais organizações são públicas?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?empresa_publica ?nome WHERE {
    {
    ?empresa_publica a sefazma:Ente_Publico;
                     rdfs:label ?nome.
    }
    UNION
    {
        ?empresa_publica a foaf:Organization;
                     rdfs:label ?nome;
                     sefazma:tem_natureza_legal ?natureza_legal.
   		?natureza_legal sefazma:codigo_natureza_legal ?codigo;
    FILTER(?codigo = "201-1"^^xsd:string || ?codigo = "2011"^^xsd:decimal)
    }
}

#Quais situações cadastrais da RFB são incompatíveis com as existentes na SEFAZ?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?estabelecimento_rfb ?estabelecimento_cadastro ?cnpj_rfb ?cnpj_cadastro ?nome_fantasia_rfb ?nome_fantasia_cadastro ?situacao_rfb ?situacao_cadastro   WHERE {

 ?estabelecimento_rfb a sefazma:Estabelecimento_RFB;
     sefazma:cnpj ?cnpj_rfb;
     sefazma:nome_fantasia ?nome_fantasia_rfb;
     sefazma:tem_situacao_cadastral ?situacao_rfb.
	
   ?estabelecimento_rfb owl:sameAs ?estabelecimento_cadastro.
    ?estabelecimento_cadastro a sefazma:Estabelecimento_Cadastro;
            sefazma:cnpj ?cnpj_cadastro;
            sefazma:nome_fantasia ?nome_fantasia_cadastro;
            sefazma:tem_situacao_cadastral ?situacao_cadastro.
    OPTIONAL {
        
        BIND(?situacao_cadastro AS ?situacao_cadastro2)
    	?situacao_rfb owl:sameAs ?situacao_cadastro2.     
    } 
    FILTER(!BOUND(?situacao_cadastro2))
  
}

# Quem são os contribuintes?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?contribuinte ?inscricao_estadual WHERE {
    ?contribuinte a sefazma:Contribuinte_Geral;
          rdfs:label ?inscricao_estadual.
}

# Quem são os contribuintes SN?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?contribuinte ?inscricao_estadual WHERE {
    ?contribuinte a sefazma:Simples_Nacional;
          rdfs:label ?inscricao_estadual.
}

# Quem são os contribuintes ST?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?contribuinte ?inscricao_estadual WHERE {
    ?contribuinte a sefazma:Substituto_Tributario;
          rdfs:label ?inscricao_estadual.
}

# Quem são os Não Contribuintes?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?contribuinte ?inscricao_estadual WHERE {
    ?contribuinte a sefazma:Nao_Contribuinte;
          rdfs:label ?inscricao_estadual.
}

# Quais Empresas na SEFAZ não tem Sócios?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?empresa WHERE {
    ?empresa a sefazma:Empresa_Cadastro.
    MINUS {
        ?empresa sefazma:tem_sociedade ?sociedade.
    }
}


# Quais são os órgãos públicos por esfera do poder (Executivo, Legislativo e Judiciário) e por nível (Federal, Estadual e Municipal)?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?ente_publico ?ente ?esfera ?nivel WHERE {
    ?ente_publico a sefazma:Ente_Publico;
                  rdfs:label ?ente;
    			  sefazma:esfera ?esfera;
                  sefazma:nivel ?nivel.
}

# Quem Vende para os órgãos públicos?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?fornecedor_compra ?cnpj_cpf WHERE {
    ?compra a sefazma:Compra_Publica;
            sefazma:tem_fornecedor ?fornecedor.
    ?fornecedor rdfs:label ?fornecedor_compra;
                sefazma:cnpj_cpf ?cnpj_cpf.
}

# Quais fornecedores estão com alguma restrição ou sanção nas fontes de dados integradas: (CEI, CEIS ou TCU)?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?fornecedor_restrito ?cnpj_cpf WHERE {
     ?fornecedor a sefazma:Fornecedor_Restrito;
                 foaf:name ?fornecedor_restrito;
                 sefazma:cnpj_cpf ?cnpj_cpf.
}

# Quais fornecedores estão com alguma restrição em uma determinada data?


PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?fornecedor ?data_transito_julgado WHERE { 
    ?processo a sefazma:Processo;
              sefazma:tem_fornecedor_restrito ?fornecedor;
              sefazma:data_transito_julgado ?data_transito_julgado;
              sefazma:data_final ?data_final_processo.
    
 FILTER(
        ?data_transito_julgado   
        = "18/10/2014"
    )

} LIMIT 100


# Quais os Fornecedores Maranheses?

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sefazma: <http://www.sefaz.ma.gov.br/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT * WHERE { 
	?fornecedor_restrito a sefazma:Fornecedor_Restrito ;
    sefazma:tem_unidade_federativa ?uf.
    
    ?uf a sefazma:Unidade_Federativa;
        rdfs:label ?estado.
    
    FILTER(?estado = "MA")
    
}
